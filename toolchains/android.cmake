# Generic Android toolchain wrapper for cmake-based deps (e.g. whisper.cpp).
# Expects ANDROID_NDK and ANDROID_ABI in the environment.

if(NOT DEFINED ANDROID_NDK)
  if(DEFINED ENV{ANDROID_NDK_HOME})
    set(ANDROID_NDK "$ENV{ANDROID_NDK_HOME}")
  elseif(DEFINED ENV{NDK})
    set(ANDROID_NDK "$ENV{NDK}")
  elseif(DEFINED ENV{CMAKE_ANDROID_NDK})
    set(ANDROID_NDK "$ENV{CMAKE_ANDROID_NDK}")
  else()
    message(FATAL_ERROR "ANDROID_NDK not set. Set ANDROID_NDK_HOME or NDK.")
  endif()
endif()

if(NOT DEFINED ANDROID_API)
  if(DEFINED ENV{ANDROID_API})
    set(ANDROID_API "$ENV{ANDROID_API}")
  elseif(DEFINED ENV{API})
    set(ANDROID_API "$ENV{API}")
  else()
    set(ANDROID_API 21)
  endif()
endif()

if(NOT DEFINED ANDROID_ABI)
  if(DEFINED ENV{ANDROID_ABI})
    set(ANDROID_ABI "$ENV{ANDROID_ABI}")
  else()
    message(FATAL_ERROR "ANDROID_ABI not set. Set ANDROID_ABI (arm64-v8a, armeabi-v7a, x86, x86_64).")
  endif()
endif()

# Vulkan-Headers: provide vulkan.hpp for ggml-vulkan when cross-compiling.
if(DEFINED ENV{VULKAN_HEADERS})
  set(_VULKAN_HEADERS_ROOT "$ENV{VULKAN_HEADERS}")
elseif(DEFINED ENV{VULKAN_SDK})
  set(_VULKAN_HEADERS_ROOT "$ENV{VULKAN_SDK}")
endif()

if(DEFINED _VULKAN_HEADERS_ROOT)
  if(EXISTS "${_VULKAN_HEADERS_ROOT}/include/vulkan/vulkan.hpp")
    set(Vulkan_INCLUDE_DIR "${_VULKAN_HEADERS_ROOT}/include" CACHE PATH "" FORCE)
    set(Vulkan_INCLUDE_DIRS "${_VULKAN_HEADERS_ROOT}/include" CACHE PATH "" FORCE)
  elseif(EXISTS "${_VULKAN_HEADERS_ROOT}/vulkan/vulkan.hpp")
    set(Vulkan_INCLUDE_DIR "${_VULKAN_HEADERS_ROOT}" CACHE PATH "" FORCE)
    set(Vulkan_INCLUDE_DIRS "${_VULKAN_HEADERS_ROOT}" CACHE PATH "" FORCE)
  endif()
endif()

set(ANDROID_PLATFORM android-${ANDROID_API})
set(CMAKE_ANDROID_NDK "${ANDROID_NDK}")
set(CMAKE_ANDROID_STL_TYPE c++_shared)

include("${ANDROID_NDK}/build/cmake/android.toolchain.cmake")
